// variable definitions
[VarDef, dllPath, "dummy"]
[VarDef, Tool_Export_Tri]
[VarDef, Tool_Polypaint_Colorize]
[VarDef, Tool_Export_Txr]
[VarDef, Tool_Export_Flp]
[VarDef, Tool_Export_Mrg]
[VarDef, Tool_Export_Grp]
// hard coded...
[VarDef, tmpFilePath, [fileNameResolvePath, "ZBRUSH_ZSTARTUP\ZPlugs64\ExactUnionData\test.obj"]]
[VarDef, unionResult]
[VarDef, path, "dummyPath"]
[VarDef, memBlock]

// check if this plugin is correctly installed
// from http://docs.pixologic.com/user-guide/customizing-zbrush/zscripting/zfileutils/#dllPath
[RoutineDef, CheckSystem,	
    //check ZBrush version
    [VarSet,Zvers,[ZBrushInfo,0]]
    [If,[Val,Zvers] >= 4.8,,		
        [Note,"\Cff9923This zscript\Cffffff is not designed for this version of \Cff9923ZBrush\Cffffff.",,3,4737096,,300]
        [Exit]
    ]
    [VarSet,isMac, [ZBrushInfo,6]]	//check Mac or PC
    // Make sure we have the dll and set its path
    [If, [ZBrushInfo, 16]==64,//64 bit
        [If, isMac,
            //use the path below for testing only   
            [VarSet, dllPath, "ExactUnionData/ExactUnion.lib"]	
            //use the path below for installed plugins
            //[VarSet, dllPath, "ZBRUSH_ZSTARTUP/ZPlugs64/ExactUnionData/ExactUnion.lib"]						
            ,
            //use the path below for testing only	
            [VarSet, dllPath, "ExactUnionData\ExactUnion64.dll"]
            //use the path below for installed plugins
            //[VarSet, dllPath, "ZBRUSH_ZSTARTUP\ZPlugs64\ExactUnionData\ExactUnion64.dll"]		
        ]
    ,//else 32 bit - no longer supported
        [Note, "\Cff9923This zscript\Cffffff is not designed for this version of \Cff9923ZBrush\Cffffff.",,3,4737096,,300]
        [Exit]
    ]
    // check if there are dll/lib exists
    [If, [FileExists, [Var,dllPath]],
    , // else no DLL.
    [Note,"\Cff9923Note :\Cc0c0c0 The \Cff9923 ExactUnion plugin \Cffffffdll/lib\Cc0c0c0 could not be found at the correct location.  Please re-install the plugin, making sure the relevant files and folders are in the \CffffffZStartup/ZPlugs\Cc0c0c0 folder."]
    [Exit]
    ]
]//end routine



// main processing
//[ISubPalette, "ZPlugin:Exact Union"]
//[IButton, "ZPlugin:Exact Union:Compute", "Compute exact union of multiple shells",
[ISubPalette, "ZPlugin:Test plugin"]
[IButton, "ZPlugin:Test plugin:Compute", "Plugin test",
    [If, [IExists, Tool:Export],
        [RoutineCall, CheckSystem]
        // store current export settings
        [VarSet, Tool_Export_Tri,[IGet, Tool:Export:Tri]]
        [VarSet, Tool_Polypaint_Colorize,[IGet, Tool:Polypaint:Colorize]]
        [VarSet, Tool_Export_Txr,[IGet, Tool:Export:Txr]]
        [VarSet, Tool_Export_Flp,[IGet, Tool:Export:Flp]]
        [VarSet, Tool_Export_Mrg,[IGet, Tool:Export:Mrg]]
        [VarSet, Tool_Export_Grp,[IGet, Tool:Export:Grp]]

        //set export options
        [IPress,Tool:Export:Tri]
        // don't need any extra attributes
        [IUnPress,Tool:Polypaint:Colorize]
        [IUnPress,Tool:Export:Txr]
        [IUnPress,Tool:Export:Flp]
        [IUnPress,Tool:Export:Mrg]
        [IUnPress,Tool:Export:Grp]

        //export current subtool to OBJ file (temporary file)
        [FileNameSetNext, [Var, tmpFilePath]]
        [IPress,Tool:Clone]
        [IPress,Tool:Export]

        // restore original settings
        [If, [Var, Tool_Export_Tri],
            [IPress, Tool:Export:Tri],
            [IPress, Tool:Export:Qud]
        ]
        [ISet, Tool:Polypaint:Colorize, [Var, Tool_Polypaint_Colorize]]
        [ISet, Tool:Export:Txr, [Var, Tool_Export_Txr]]
        [ISet, Tool:Export:Flp, [Var, Tool_Export_Flp]]
        [ISet, Tool:Export:Mrg, [Var, Tool_Export_Mrg]]
        [ISet, Tool:Export:Grp, [Var, Tool_Export_Grp]]
    
        // do something with C++
        [Varset, unionResult, [FileExecute, [Var, dllPath], "computeUnion", [var, tmpFilePath]]]

        // import to Tool
        [FileNameSetNext,[Var, tmpFilePath]]
        [IPress,Tool:Import]

        // erase temporary file.
        [FileDelete, [Var, tmpFilePath]]
        ,
    ]
]
